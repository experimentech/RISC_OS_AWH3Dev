; This source code in this file is licensed to You by Castle Technology
; Limited ("Castle") and its licensors on contractual terms and conditions
; ("Licence") which entitle you freely to modify and/or to distribute this
; source code subject to Your compliance with the terms of the Licence.
; 
; This source code has been made available to You without any warranties
; whatsoever. Consequently, Your use, modification and distribution of this
; source code is entirely at Your own risk and neither Castle, its licensors
; nor any other person who has contributed to this source code shall be
; liable to You for any loss or damage which You may suffer as a result of
; Your use, modification or distribution of this source code.
; 
; Full details of Your rights and obligations are set out in the Licence.
; You should have received a copy of the Licence with this source code file.
; If You have not received a copy, the text of the Licence is available
; online at www.castle-technology.co.uk/riscosbaselicence.htm
; 
; -> hdr.MyMacros


; =============================================
; Macros should be kept in ALPHABETICAL order !
; =============================================

;*************************************************
; MOVES REGISTER TO NEXT ALIGNMENT OR STAYS WHERE IT IS
; Flags and $temp corrupted
;*************************************************
        MACRO
$label  ALIGNREG $reg
$label  TST   $reg, #3
        ADDNE $reg, $reg, #4
        BICNE $reg, $reg, #3
        MEND

; ************************************
; ***  Call the byte copy routine  ***
; ************************************

; Note: This is purely for ease rather than elegance, sorry (JRF)
        MACRO
$var    CD_ByteCopy  $cond
 [ UseSWICDByteCopy
        SWI$cond     XCD_ByteCopy
 |
        BL$cond      MyCD_ByteCopy
 ]
        MEND

;********************************************************
;********************************************************

; This converts an ISO or HISIERRA date to an ARCHY centi-second date

;********************************************************
;********************************************************
; $from -> 6 byte block, $to -> put 5 byte Archy block

        MACRO
$label  ConvertToArchyDate $from, $to, $temp1, $templo, $temp3, $temp4, $temphi
$label  LDRB    $temp1, [ $from, #0 ]              ; R1 = year ( from 1900 )
                
        LDR     $temp3, =365                       ; ( year * 365 ) + ( year DIV 4 )
        MOV     $temp4, $temp1, ASR #2             ;
        MLA     $temp3, $temp1, $temp3, $temp4     ;
                
        LDR     $temp1, =6 * 15 * 25 * 15          ; result * numberofcentisecondsaday / 256
        MUL     $temp3, $temp1, $temp3             ;
                
        MOV     $temphi, $temp3, ASR #24           ; h/b of result ( ignore bits 8 to 31 )
        MOV     $templo, $temp3, ASL #8            ; l/bs of result
                
        LDRB    $temp3, [ $from, #1 ]              ; month ( 1 to 12 )
        ADRL    $temp4, DaysInMonth - 4            ; R3 = cumulative total of days up to that month
        LDR     $temp3, [ $temp4, $temp3, ASL #2 ] ;

        LDRB    $temp4, [ $from, #2 ]              ; day of month
        ADD     $temp3, $temp3, $temp4             ;
        MUL     $temp4, $temp3, $temp1             ; R4 = days * 6 * 15 * 15 * 25
                
        ADD     $templo, $templo, $temp4, ASL #8   ; l/bs of result
        MOV     $temp3, $temp4, ROR #24            ; h/b of result
        AND     $temp3, $temp3, #&FF               ;
        ADD     $temphi, $temphi, $temp3           ;
                
        LDRB    $temp3, [ $from, #3 ]              ; l/bs hourcentiseconds
        LDR     $temp1, =60*60*100                 ;
        MLA     $templo, $temp1, $temp3, $templo   ;
                
        LDR     $temp1, =60*100                    ; l/bs minutecentiseconds
        LDRB    $temp3, [ $from, #4 ]              ;
        MLA     $templo, $temp1, $temp3, $templo   ;
                
        LDRB    $temp1, [ $from, #5 ]              ; l/bs of secondcentiseconds
        MOV     $temp3, #100                       ;
        MLA     $templo, $temp1, $temp3, $templo   ;
                
        STR     $templo, [ $to ]
        STRB    $temphi, [ $to, #4 ]
        MEND

; **********************************************************
; ***                  Unsigned divide by 10             ***
; on entry:
;          $number = value to divide by 10
; on exit:
;          $number=$number / 10
;          $temp1 =$number MOD 10
; **********************************************************
        MACRO
$label  DivideBy10 $number, $temp1, $temp3
$label  MOV     $temp1,$number
        MOV     $number,$number,LSR #1
        ADD     $number,$number,$number,LSR #1
        ADD     $number,$number,$number,LSR #4
        ADD     $number,$number,$number,LSR #8
        ADD     $number,$number,$number,LSR #16
        MOV     $number,$number,LSR #3
        ADD     $temp3,$number,$number,LSL #2
        SUB     $temp1,$temp1,$temp3,LSL #1
        CMP     $temp1,#10
        ADDGE   $number,$number,#1
        SUBGE   $temp1,$temp1,#10
        MEND

;***************************************************************************
; Exchange reserved characters for non reserved ones
;***************************************************************************
        MACRO
$label  ReplaceBadCharacters  $reg
$label  TEQ     $reg,#SPACE
        MOVEQ   $reg,#REPLACEMENTFORSPACE
        TEQ     $reg,#DOT
        MOVEQ   $reg,#REPLACEMENTFORDOT
        TEQ     $reg,#WILDCHAR
        TEQNE   $reg,#WILDANY
        MOVEQ   $reg,#REPLACEMENTFORWILD
        TEQ     $reg,#SYSTEMQUOTE
        TEQNE   $reg,#SYSTEMROOT
        TEQNE   $reg,#SYSTEMLIB
        TEQNE   $reg,#SYSTEMURD
        TEQNE   $reg,#SYSTEMCSD
        TEQNE   $reg,#SYSTEMCOLON
        TEQNE   $reg,#SYSTEMLT
        TEQNE   $reg,#SYSTEMGT
        TEQNE   $reg,#SYSTEMDEL
        MOVEQ   $reg,#REPLACEMENTFORSYSTEM
        MEND

;***********************************************************************
; This pulls from a full-descending stack ( -> R13 )
;***********************************************************************

        MACRO
$label  PushAllWithReturnFrame $cond
$label  Push     "R0-R11,R14", $cond
        STR$cond sp, stackreturn        ; Return frame, no reentrancy!
        MEND

        MACRO
$label  PullAllFromFrame $cond
$label  LDR$cond sp, stackreturn
        Pull     "R0-R11,R14", $cond
        MEND

        MACRO
$label  PullAllFromFrameAndExit $cond, $keep
$label  LDR$cond sp, stackreturn
      [ "$keep"=""
        Pull     "R0-R11,PC", $cond
      |
        ; Keep the first $keep registers
        ADD$cond sp, sp, #4 * $keep
        DCI      (Cond_$cond :OR: &08BD8FFF) :AND: :NOT: ((1:SHL:$keep) - 1)
      ]
        MEND

; ******************************************************
; ***  Macro Find length of null terminated string   ***
; ******************************************************
        MACRO
$label  LengthOfString $name, $length, $temp
        MOV     $length, #0
01              
$label  LDRB    $temp, [ $name, $length ]
        ADD     $length, $length, #1
        CMP     $temp, #0
        BNE     %BT01
        MEND

; ******************************************************
; ***  This is for cdfs to keep a log of calls made  ***
; ******************************************************
        MACRO
$label log_on
$label
      [ log
        Push    "R14"
        LDR     R14, log_pointer
        STR     PC, [ R14 ], #4
        STR     R14, log_pointer
        Pull    "R14"
      ]
      MEND

; ************************************************************************************
; ***  This macro is specialised for XA and is used in 'FileMan' and 'EntryFile'   ***
; *** on entry:                                                                    ***
; ***          $return  =  UNUSED                                                  ***
; ***          $pointer -> object information                                      ***
; ***          $temp1   =  UNUSED                                                  ***
; ***          $temp2   =  UNUSED                                                  ***
; *** on exit:                                                                     ***
; ***          $return  =  1, 2 or 3                                               ***
; ***          $pointer -> preserved                                               ***
; ***          $temp1   =  preserved                                               ***
; ***          $temp2   =  preserved                                               ***
; ************************************************************************************

        MACRO
$label  MACRO__XA_WHAT_DATA_MODE $return, $pointer, $temp1, $temp2
$label  Push    "$temp1, $temp2"
        MOV     $return, # 1
        LDRB    $temp1, [ $pointer, # OBJECT__XA_ATTRIBUTES_LO ]
        LDRB    $temp2, [ $pointer, # OBJECT__XA_ATTRIBUTES_HI ]
        ORR     $temp1, $temp1, $temp2, LSL # 8
        TEQ     $temp1, # 0             ; [ no XA information ]
        BEQ     %FT01
        ; Mode 2 form 2 ?
        TST     $temp1, # ATTRIBUTES__XA_MODE_2_FORM_2
        MOVNE   $return, # 2
        MOVEQ   $return, # 3
01
        Pull    "$temp1, $temp2"
        MEND

        END
